CMAKE_MINIMUM_REQUIRED (VERSION 2.6)
PROJECT (urltest_webdav C)

#
# Program version:
#
SET(URLTEST_WEBDAV_VERSION_STRING "1.0.0")

#
# Cached variables for this project:
#
SET(CURL_PREFIX "" CACHE PATH "Installation path for libcurl")

#
# GNU-like installation:
#
INCLUDE(GNUInstallDirs)

#
# For finding packages and functions:
#
INCLUDE(FindPackageHandleStandardArgs)
INCLUDE(CheckFunctionExists)

#
# Locate libcurl:
#
IF (CURL_PREFIX)
  LIST(APPEND CMAKE_PREFIX_PATH ${CURL_PREFIX})
ENDIF (CURL_PREFIX)
FIND_PATH(LIBCURL_INCLUDE_DIR NAMES curl/curl.h)
FIND_LIBRARY(LIBCURL_LIBRARY NAMES libcurl.so libcurl.dylib)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(LIBCURL DEFAULT_MSG LIBCURL_LIBRARY LIBCURL_INCLUDE_DIR)
IF (LIBCURL_FOUND)
	SET(LIBCURL_LIBRARIES ${LIBCURL_LIBRARY})
	SET(LIBCURL_INCLUDE_DIRS ${LIBCURL_INCLUDE_DIR})
ELSE (LIBCURL_FOUND)
	MESSAGE(FATAL_ERROR "libcurl could not be found")
ENDIF (LIBCURL_FOUND)
MARK_AS_ADVANCED(LIBCURL_INCLUDE_DIRS LIBCURL_LIBRARIES)

#
# Use random() or rand()?
#
CHECK_FUNCTION_EXISTS (srandomdev HAVE_SRANDOMDEV)
IF (HAVE_SRANDOMDEV)
	ADD_DEFINITIONS(-DHAVE_SRANDOMDEV -DHAVE_RANDOM)
ELSE (HAVE_SRANDOMDEV)
	CHECK_FUNCTION_EXISTS (srandom HAVE_SRANDOM)
	IF (HAVE_SRANDOM)
		ADD_DEFINITIONS(-DHAVE_SRANDOM -DHAVE_RANDOM)
	ENDIF (HAVE_SRANDOM)
ENDIF (HAVE_SRANDOMDEV)

#
# Is asprintf() present?
#
CHECK_FUNCTION_EXISTS (asprintf HAVE_ASPRINTF)
IF (HAVE_ASPRINTF)
	ADD_DEFINITIONS(-DHAVE_ASPRINTF)
ENDIF (HAVE_ASPRINTF)

#
# The executable:
#
ADD_EXECUTABLE(urltest_webdav fs_entity.c http_ops.c http_stats.c util_fns.c urltest_webdav.c)
ADD_DEFINITIONS(-DURLTEST_WEBDAV_VERSION_STRING="\"\"${URLTEST_WEBDAV_VERSION_STRING}\"\"")
TARGET_LINK_LIBRARIES(urltest_webdav -lm ${LIBCURL_LIBRARIES})

